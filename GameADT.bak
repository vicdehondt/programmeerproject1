(define (make-game level)
  (let ((draw (make-draw))
        (score 0)
        (lives 3)
        (highscore (read-file "highscore.txt"))
        (game-over? #f)
        (running #f))

    (define (start-up!)
      (draw 'show-splash! key-callback))

    (define (start-game!)
      (draw 'start! game-loop dispatch))

    ;; What happens every tick
    (define (game-loop delta-time)
      (if (not game-over?)
          (begin
            (draw 'update! dispatch)
            (update-score!)
            (update-lives!)
            (check-game-over)
            (level 'move-scorpion! delta-time)
            (level 'check-for-ant-scorpion-collision))))

    ;; What to do when a key is pressed
    (define (key-callback status key)
      (if (eq? status 'pressed)
          (begin
            (level 'move-ant! key)
            (start-game? key))))

    (define (update-score!)
      (if (level 'update-score?)
          (begin
            (level 'update-score! #f)
            (set! score (+ score 500)))))

    (define (update-lives!)
      (if (level 'remove-live?)
          (begin
            (level 'remove-live! #f)
            (set! lives (- lives 1)))))

    (define (start-game? key)
      (if (not running)
          (if (eq? key #\space)
              (begin
                (start-game!)
                (set! running #t)))))

    (define (check-game-over)
      (if (<= lives 0)
          (begin
            (set! game-over? #t)
            (draw 'game-over!))))

    (define (dispatch message . parameters)
      (cond
        ((eq? message 'start!) (start-up!))
        ((eq? message 'level) level)
        ((eq? message 'score) score)
        ((eq? message 'highscore) highscore)
        ((eq? message 'lives) lives)
        (else  (error "[ERROR in GameADT DISPATCH] Wrong message: ") (display message))))

    dispatch))
