(define (make-visual window)
  (let ((ant-sequence (make-tile-sequence (list (make-bitmap-tile "images/48px/FireAnt-Up.png" "images/48px/FireAnt-Up-mask.png")
                                                (make-bitmap-tile "images/48px/FireAnt-Right.png" "images/48px/FireAnt-Right-mask.png")
                                                (make-bitmap-tile "images/48px/FireAnt-Down.png" "images/48px/FireAnt-Down-mask.png")
                                                (make-bitmap-tile "images/48px/FireAnt-Left.png" "images/48px/FireAnt-Left-mask.png"))))
        (wall-tiles '())
        (egg-tiles '())
        (scorpion-tiles '()))
    

    ;;
    ;; Layer initialization
    ;;
    (define base-layer (window 'make-layer))
    (define egg-layer (window 'make-layer))
    (define moving-objects-layer (window 'make-layer))


    ;;
    ;; DRAW PROCEDURES
    ;;

    ;; Show ant on screen
    ((moving-objects-layer 'add-drawable) ant-sequence)

    ;; Lets the tile look to the direction it's going
    (define (set-sequence! object sequence)
      (let ((current-orientation (object 'orientation)))
        (cond
          ((and (eq? current-orientation 'right) (eq? (object 'previous-orientation) 'left)) (object 'previous-orientation! current-orientation)
                                                                                        (sequence 'set-next!))
          ((and (eq? current-orientation 'left) (eq? (object 'previous-orientation) 'right)) (object 'previous-orientation! current-orientation)
                                                                                        (sequence 'set-previous!)))))

    (define (set-current-tile! orientation sequence)
      (cond
        ((eq? orientation 'up) ((sequence 'set-current!) 0))
        ((eq? orientation 'right) ((sequence 'set-current!) 1))
        ((eq? orientation 'down) ((sequence 'set-current!) 2))
        ((eq? orientation 'left) ((sequence 'set-current!) 3))
        (else (error "[ERROR in VisualADT/set-current-tile!] Wrong orientation: ") (display orientation))))

    (define (draw-ant! level-object)
      (let* ((ant (level-object 'ant)))
        (set-current-tile! (ant 'orientation) ant-sequence)
        (draw-object! ant ant-sequence)))

    (define (draw-walls! level-object)
      (for-each-object (lambda (wall) (draw-stationary-piece! wall level-object)) (level-object 'walls)))

    (define (draw-eggs! level-object)
      (for-each-object (lambda (egg) (draw-stationary-piece! egg level-object)) (level-object 'eggs)))

    (define (draw-scorpions! level-object)
      (for-each-object (lambda (scorpion) (draw-scorpion-piece! scorpion)) (level-object 'scorpions)))

    (define (draw-scorpion-piece! scorpion-object)
      (let ((sequence (get-object-piece scorpion-object))
            (orientation (scorpion-object 'orientation)))
        (set-current-tile! orientation sequence)
        (draw-object! scorpion-object sequence)))

    (define (draw-stationary-piece! object level-object)
      (let ((tile (get-object-piece object)))
        (draw-object! object tile)))

    (define (which-tiles-list kind)
      (cond
        ((eq? kind 'wall) wall-tiles)
        ((eq? kind 'egg) egg-tiles)
        ((eq? kind 'scorpion) scorpion-tiles)))

    ;; Looks for the right tile bound to the given object
    (define (get-object-piece object)
      (let* ((tile-list (which-tiles-list (object 'kind)))
             (result (assoc object tile-list)))
        (if result
            (cdr result)
            (add-object-piece! object))))

    (define (add-object-piece! object)
      (let* ((kind (object 'kind))
             (new-scorpion-sequence (make-tile-sequence (list (make-bitmap-tile "images/48px/Scorpion-Up.png" "images/48px/Scorpion-Up-mask.png")
                                                              (make-bitmap-tile "images/48px/Scorpion-Right.png" "images/48px/Scorpion-Right-mask.png")
                                                              (make-bitmap-tile "images/48px/Scorpion-Down.png" "images/48px/Scorpion-Down-mask.png")
                                                              (make-bitmap-tile "images/48px/Scorpion-Left.png" "images/48px/Scorpion-Left-mask.png"))))
             (new-egg-tile (make-bitmap-tile "images/48px/Egg.png" "images/48px/Egg-mask.png"))
             (new-wall-tile (make-tile 48 48 "images/48px/Wall.png")))
        
        (define (add-piece new-tile)
          (add-object-tile! object new-tile)
          (show! new-tile kind)
          new-tile)
        
        (cond
          ((eq? kind 'scorpion) (add-piece new-scorpion-sequence))
          ((eq? kind 'egg) (add-piece new-egg-tile))
          ((eq? kind 'wall) (add-piece new-wall-tile))
          (else (error "[ERROR in VisualADT/add-object-piece!] Wrong kind: ") (display kind)))))

    (define (draw-object! obj tile)
      (let* ((position (obj 'position))
             (new-x (* (position 'x) grid-cell))
             (new-y (* (position 'y) grid-cell)))
        ((tile 'set-x!) new-x)
        ((tile 'set-y!) new-y)))

    ;; Help procedure for adding an object and its tile
    (define (add-object-tile! object tile )
      (let ((kind (object 'kind)))
        (cond
          ((eq? kind 'scorpion) (set! scorpion-tiles (cons (cons object tile) scorpion-tiles)))
          ((eq? kind 'egg) (set! egg-tiles (cons (cons object tile) egg-tiles)))
          ((eq? kind 'wall) (set! wall-tiles (cons (cons object tile) wall-tiles)))
          (else (error "[ERROR in VisualADT/add-object-tile!] Wrong kind: ") (display kind)))))

    (define (show! tile kind)
      (cond
        ((eq? kind 'scorpion) ((moving-objects-layer 'add-drawable) tile))
        ((eq? kind 'egg) ((egg-layer 'add-drawable) tile))
        ((eq? kind 'wall) ((base-layer 'add-drawable) tile))
        (else (error "[ERROR in VisualADT/show!] Wrong kind: ") (display kind))))

    
    ;;
    ;; EGG REMOVAL
    ;;

    ;; Finds the right egg to remove
    (define (which-egg-to-remove level-object)
      (let find ((current (car egg-tiles))
                 (remaining (cdr egg-tiles)))
        (if (not (member (car current) (level-object 'eggs)))
            (begin
              ((egg-layer 'remove-drawable) (cdr current))
              current)
            (find (car remaining) (cdr remaining)))))

    ;; Checks if an egg is removed
    (define (check-for-egg-remove level-object)
      (if (> (length egg-tiles) (length (level-object 'eggs)))
          (set! egg-tiles (remove-from-list (which-egg-to-remove level-object) egg-tiles))))


    ;;
    ;; PUBLIC PROCEDURES
    ;;

    (define (start! level-object)
      (draw-walls! level-object))
    
    (define (update! level-object)
      (draw-ant! level-object)
      (draw-scorpions! level-object)
      (draw-eggs! level-object)
      (check-for-egg-remove level-object))

    (lambda (message . parameters)
      (cond
        ((eq? message 'start!) (apply start! parameters))
        ((eq? message 'update!) (apply update! parameters))
        (else (error "[ERROR in VisualADT DISPATCH] Wrong message: ") (display message))))))